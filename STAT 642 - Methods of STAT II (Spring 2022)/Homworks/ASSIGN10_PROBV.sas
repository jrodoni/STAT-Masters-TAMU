
ods html;ods graphics on;
*Exercise 15.2 from the textbook;

option ls=90 ps=55 nocenter nodate; 
title 'ASSIGNMENT 10 - PROBLEM V';
data RAW; 
INPUT DEN $ MO $ UNIT $ @;
DO DAY = 1 TO 3;    
INPUT Y @; OUTPUT; END;
LABEL DEN = 'DENSITY' MO = 'MOISTURE'  Y='CO2 EVOLUTION';
cards;                                                             
 1.1 0.10 1  2.70 0.34 0.11
 1.1 0.10 2  2.90 1.57 1.25
 1.1 0.20 3  5.20 5.04 3.70
 1.1 0.20 4  3.60 3.92 2.69
 1.1 0.24 5  4.00 3.47 3.47
 1.1 0.24 6  4.10 3.47 2.46

 1.4 0.10 7  2.60 1.12 0.90
 1.4 0.10 8  2.20 0.78 0.34
 1.4 0.20 9  4.30 3.36 3.02
 1.4 0.20 10 3.90 2.91 2.35
 1.4 0.24 11 1.90 3.02 2.58
 1.4 0.24 12 3.00 3.81 2.69

 1.6 0.10 13 2.00 0.67 0.22
 1.6 0.10 14 3.00 0.78 0.22
 1.6 0.20 15 3.80 2.80 2.02
 1.6 0.20 16 2.60 3.14 2.46
 1.6 0.24 17 1.30 2.69 2.46
 1.6 0.24 18 0.50 0.34 0.00

RUN;
PROC PRINT DATA=RAW;
VAR DEN MO UNIT DAY Y;
RUN;
TITLE2 'ANALYSIS AS A SPLIT PLOT DESIGN-PROC MIXED TYPE=CS';
PROC MIXED DATA=RAW ;
 CLASS DEN MO UNIT DAY;
 MODEL Y = DEN MO DEN*MO DAY DEN*DAY MO*DAY DEN*MO*DAY;
 RANDOM UNIT(DEN*MO);
 REPEATED DAY/SUB=UNIT(DEN*MO) TYPE=CS R RCorr;
 CONTRAST 'LINEAR MO=.1'  DAY -1 0 1 MO*DAY -1  0 1 0 0 0 0 0 0;
 CONTRAST  'QUAD MO=.1'   DAY 1 -2 1 MO*DAY  1 -2 1 0 0 0 0 0 0;
 CONTRAST 'LINEAR MO=.2'  DAY -1 0 1 MO*DAY  0 0 0 -1  0 1 0 0 0;
 CONTRAST  'QUAD MO=.2'   DAY 1 -2 1 MO*DAY 0 0 0  1 -2 1 0 0 0;
 CONTRAST 'LINEAR MO=.24'  DAY -1 0 1 MO*DAY  0 0 0  0  0 0 -1  0 1;
 CONTRAST  'QUAD MO=.24'   DAY 1 -2 1 MO*DAY 0 0 0  0  0 0 1 -2 1 ;
 LSMEANS  MO MO*DAY MO*DEN/ADJUST=TUKEY;
RUN;
TITLE2 'ANALYSIS AS A REPEATED MEASURES DESIGN- WITH AR(1) STRUCTURE ACROSS TIME';
PROC MIXED DATA=RAW;
 CLASS DEN MO UNIT DAY;
 MODEL Y = DEN MO DEN*MO DAY DEN*DAY DAY*MO  DEN*MO*DAY;
 RANDOM UNIT(DEN*MO);
 REPEATED DAY/SUB=UNIT(DEN*MO) TYPE=AR(1) R RCorr;
LSMEANS MO*DAY /pdiff;
LSMEANS MO*DAY /adjust=tukey;
CONTRAST 'LINEAR MO=.1'  DAY -1 0 1 MO*DAY -1  0 1 0 0 0 0 0 0;
 CONTRAST  'QUAD MO=.1'   DAY 1 -2 1 MO*DAY  1 -2 1 0 0 0 0 0 0;
 CONTRAST 'LINEAR MO=.2'  DAY -1 0 1 MO*DAY  0 0 0 -1  0 1 0 0 0;
 CONTRAST  'QUAD MO=.2'   DAY 1 -2 1 MO*DAY 0 0 0  1 -2 1 0 0 0;
 CONTRAST 'LINEAR MO=.24'  DAY -1 0 1 MO*DAY  0 0 0  0  0 0 -1  0 1;
 CONTRAST  'QUAD MO=.24'   DAY 1 -2 1 MO*DAY 0 0 0  0  0 0 1 -2 1 ;
CONTRAST 'LINEAR DEN=1.1'  DAY -1 0 1 DEN*DAY -1  0 1 0 0 0 0 0 0;
 CONTRAST  'QUAD DEN=1.1'   DAY 1 -2 1 DEN*DAY  1 -2 1 0 0 0 0 0 0;
 CONTRAST 'LINEAR DEN=1.4'  DAY -1 0 1 DEN*DAY  0 0 0 -1  0 1 0 0 0;
 CONTRAST  'QUAD DEN=1.4'   DAY 1 -2 1 DEN*DAY 0 0 0  1 -2 1 0 0 0;
 CONTRAST 'LINEAR DEN=1.6'  DAY -1 0 1 DEN*DAY  0 0 0  0  0 0 -1  0 1;
 CONTRAST  'QUAD DEN=1.6'   DAY 1 -2 1 DEN*DAY 0 0 0  0  0 0 1 -2 1 ;
 LSMEANS  MO MO*DAY MO*DEN/ADJUST=TUKEY;
 LSMEANS  DEN*DAY*MO/ADJUST=TUKEY;
 RUN;

TITLE2 'ANALYSIS AS A MULTIVARIATE DESIGN-PROC GLM';
*Need to reconfigure data set:;
DATA MULTIVARDATA; SET RAW;
 IF DAY=1 THEN DO; G1=Y;
    RETAIN G1; END;
 IF DAY=2 THEN DO; G2=Y;
    RETAIN G2; END;
 IF DAY=3 THEN DO; G3=Y;
    RETAIN G3; END;
   DROP OBS G;
   IF DAY=3;
 RUN;
PROC PRINT DATA=MULTIVARDATA;
VAR UNIT DEN MO G1-G3;
RUN;
PROC GLM DATA=MULTIVARDATA;
 CLASS DEN MO UNIT;
 MODEL G1-G3 = DEN MO DEN*MO / NOUNI;
 MANOVA H=DEN MO DEN*MO;
 REPEATED DAY 3 (1 2 3) POLYNOMIAL / PRINTE SUMMARY;
 LSMEANS DEN MO DEN*MO/STDERR PDIFF ADJUST=TUKEY;
RUN;
ods graphics off; ods html close; 
